<!DOCTYPE html>
<html>
  <head>
    <style>
      body {
          margin: 0;
          height: 100%;
          background: #111;
      }

      canvas {
          height: 100% !important;
          width: 100% !important;
          display: block;
      }
    </style>
    <script id="vertexShader" type="x-shader/x-vertex">
varying vec3 vWorldNormal;
varying vec3 vWorldPosition;

void main() {
  vWorldNormal = (vec4(normalMatrix * normal, 0) * viewMatrix).xyz;
  vWorldPosition = (modelMatrix * vec4(position, 1)).xyz;

  gl_Position = projectionMatrix * modelViewMatrix * vec4(position, 1);
}
    </script>

    <script id="fragmentShader" type="x-shader/x-fragment">
uniform samplerCube envMap;

varying vec3 vWorldPosition;
varying vec3 vWorldNormal;

// https://www.shadertoy.com/view/ll2GD3
vec3 pal( in float t, in vec3 a, in vec3 b, in vec3 c, in vec3 d ) {
  return a + b*cos( 6.28318*(c*t+d) );
}
vec3 spectrum(float n) {
  return pal( n, vec3(0.5,0.5,0.5),vec3(0.5,0.5,0.5),vec3(1.0,1.0,1.0),vec3(0.0,0.33,0.67) );
}

vec3 sampleRay(vec3 ray, vec3 normal, float ri) {
  vec3 dir = refract(ray, normal, ri);
  if (dir == vec3(0)) {
    return vec3(1);
  }
  return textureCube( envMap, dir ).rgb;
}

const float DISPERSION_SAMPLES = 10.;

void main(void) {
  vec3 normal = vWorldNormal;
  vec3 ray = normalize(vWorldPosition - cameraPosition);

  float refractionRatio = .99;
  float dispersion = .6;
  float dispersionBlendMultiplier = 20.;

  vec3 col = vec3(0);

  for (float i = 0.; i < DISPERSION_SAMPLES; i++) {
    float wavelength = i / DISPERSION_SAMPLES;
    float riMax = refractionRatio * (1. + dispersion);
    float ri = mix(refractionRatio, riMax, wavelength);
    
    vec3 sample = sampleRay(ray, normal, ri);
      
    sample *= spectrum(wavelength - .15);
    sample /= DISPERSION_SAMPLES / dispersionBlendMultiplier;
    col += sample / 10.;
  }

  vec3 col2 = sampleRay(ray, normal, .7);
  vec3 highlights = vec3(smoothstep(1.65, 1.7, length(col2))) * .1;
  col = col + (col * highlights * 2.);

  gl_FragColor = vec4(col, 1.0);
}
    </script>
    <script src="lib/three.js"></script>
    <script src="lib/TrackballControls.js"></script>
    <script src="lib/seedrandom.min.js"></script>
  </head>
  <body>
    <script src="main.js"></script>
  </body>
</html>
